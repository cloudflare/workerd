load("@aspect_rules_ts//ts:defs.bzl", "ts_config", "ts_project")
load("@//:build/wd_api_bundle.bzl", "wd_api_bundle")

modules = glob(["*.ts"])

internal_modules = glob(["internal/*.ts"])

ts_config(
    name = "node@tsconfig",
    src = ":tsconfig.json",
)

ts_project(
    name = "node",
    srcs = modules + internal_modules,
    tsconfig = "node@tsconfig",
)

wd_api_bundle(
    name = "bundle",
    # builtin modules are accessible under "node:<module_name>" name
    builtin_modules = dict([(
        m.removesuffix(".ts") + ".js",
        "node:" + m.removesuffix(".ts"),
    ) for m in modules]),
    const_name = "nodeBundle",
    include_prefix = "node",
    # internal modules are accessible under "node-internal:<module_name>" name without "internal/"
    # folder prefix.
    internal_modules = dict([(
        m.removesuffix(".ts") + ".js",
        "node-internal:" + m.removeprefix("internal/").removesuffix(".ts"),
    ) for m in internal_modules]),
    schema_id = "0xbcc8f57c63814005",
)
