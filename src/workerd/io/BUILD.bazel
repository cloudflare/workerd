load("@bazel_skylib//rules:common_settings.bzl", "bool_flag")
load("//:build/capnp_embed.bzl", "capnp_embed")
load("//:build/kj_test.bzl", "kj_test")
load("//:build/wd_cc_capnp_library.bzl", "wd_cc_capnp_library")
load("//:build/wd_cc_library.bzl", "wd_cc_library")

# Flag to enable WebGPU support via the Dawn library
bool_flag(
    name = "enable_experimental_webgpu",
    build_setting_default = False,
)

config_setting(
    name = "set_enable_experimental_webgpu",
    flag_values = {"enable_experimental_webgpu": "True"},
)

# TODO(cleanup): Split up into smaller targets, although this target is already relatively small and
# not encumbered with many dependencies.
wd_cc_library(
    name = "io-helpers",
    srcs = [
        "io-thread-context.c++",
        "io-timers.c++",
        "request-tracker.c++",
    ],
    hdrs = [
        "io-thread-context.h",
        "io-timers.h",
        "request-tracker.h",
    ],
    visibility = ["//visibility:public"],
    deps = [
        "//src/workerd/jsg",
        "@capnp-cpp//src/capnp/compat:http-over-capnp",
    ],
)

wd_cc_library(
    name = "io",
    # HACK: Currently, the `io` and `api` packages are interdependent. We fold all the sources
    #   from `api` into `io`. In principle, it should be possible to pull them apart so `api`
    #   depends on `io` but not vice versa. In practice, this appears very difficult due to the
    # IoContext -> Worker -> ServiceWorkerGlobalScope -> (various api targets) dependency chain.
    # TODO(cleanup): Fix this.
    srcs = [
        "compatibility-date.c++",
        "features.c++",
        "hibernation-manager.c++",
        "io-context.c++",
        "io-own.c++",
        "worker.c++",
    ] + ["//src/workerd/api:srcs"],
    hdrs = [
        "compatibility-date.h",
        "features.h",
        "hibernation-manager.h",
        "io-context.h",
        "io-own.h",
        "promise-wrapper.h",
        "worker.h",
    ] + ["//src/workerd/api:hdrs"],
    defines = select({
        ":set_enable_experimental_webgpu": ["WORKERD_EXPERIMENTAL_ENABLE_WEBGPU"],
        "//conditions:default": [],
    }),
    implementation_deps = [
        "//src/workerd/util:perfetto",
        "@capnp-cpp//src/kj/compat:kj-brotli",
        "@capnp-cpp//src/kj/compat:kj-gzip",
        "@nbytes",
        "@simdutf",
    ],
    visibility = ["//visibility:public"],
    deps = [
        ":actor",
        ":actor-id",
        ":actor-storage_capnp",
        ":capnp",
        ":io-channels",
        ":io-gate",
        ":io-helpers",
        ":limit-enforcer",
        ":observer",
        ":trace",
        ":worker-interface",
        "//src/cloudflare",
        "//src/node",
        "//src/workerd/api:analytics-engine_capnp",
        "//src/workerd/api:data-url",
        "//src/workerd/api:deferred-proxy",
        "//src/workerd/api:r2-api_capnp",
        "//src/workerd/jsg",
        "//src/workerd/util:autogate",
        "//src/workerd/util:sqlite",
        "//src/workerd/util:thread-scopes",
        "//src/workerd/util:uuid",
        "@capnp-cpp//src/capnp:capnp-rpc",
        "@capnp-cpp//src/capnp/compat:http-over-capnp",
        "@capnp-cpp//src/kj:kj-async",
        "@ssl",
    ] + select({
        ":set_enable_experimental_webgpu": ["@dawn"],
        "//conditions:default": [],
    }),
)

# TODO(cleanup): Split this up further.
wd_cc_library(
    name = "actor",
    srcs = [
        "actor-cache.c++",
        "actor-sqlite.c++",
        "actor-storage.c++",
    ],
    hdrs = [
        "actor-cache.h",
        "actor-sqlite.h",
        "actor-storage.h",
    ],
    visibility = ["//visibility:public"],
    deps = [
        ":actor-storage_capnp",
        ":io-gate",
        "//src/workerd/jsg:exception",
        "//src/workerd/util:duration-exceeded-logger",
        "//src/workerd/util:sqlite",
        "@capnp-cpp//src/capnp:capnp-rpc",
        "@capnp-cpp//src/kj:kj-async",
    ],
)

wd_cc_library(
    name = "trace",
    srcs = ["trace.c++"],
    hdrs = ["trace.h"],
    visibility = ["//visibility:public"],
    deps = [
        ":worker-interface_capnp",
        "//src/workerd/jsg:memory-tracker",
        "//src/workerd/util:own-util",
        "@capnp-cpp//src/capnp:capnp-rpc",
        "@capnp-cpp//src/capnp:capnpc",
        "@capnp-cpp//src/kj:kj-async",
    ],
)

wd_cc_library(
    name = "observer",
    srcs = ["observer.c++"],
    hdrs = ["observer.h"],
    visibility = ["//visibility:public"],
    deps = [
        ":features_capnp",
        ":trace",
        ":worker-interface",
        "//src/workerd/jsg:observer",
        "//src/workerd/util:sqlite",
    ],
)

wd_cc_library(
    name = "io-gate",
    srcs = ["io-gate.c++"],
    hdrs = ["io-gate.h"],
    visibility = ["//visibility:public"],
    deps = [
        "@capnp-cpp//src/kj",
        "@capnp-cpp//src/kj:kj-async",
    ],
)

wd_cc_library(
    name = "limit-enforcer",
    hdrs = ["limit-enforcer.h"],
    visibility = ["//visibility:public"],
    deps = [
        ":observer",
        "//src/workerd/jsg",
    ],
)

wd_cc_library(
    name = "worker-entrypoint",
    srcs = ["worker-entrypoint.c++"],
    hdrs = ["worker-entrypoint.h"],
    visibility = ["//visibility:public"],
    deps = [
        ":io",
        "//src/workerd/util:perfetto",
    ],
)

wd_cc_library(
    name = "worker-interface",
    srcs = ["worker-interface.c++"],
    hdrs = ["worker-interface.h"],
    visibility = ["//visibility:public"],
    deps = [
        ":worker-interface_capnp",
        "@capnp-cpp//src/capnp:capnp-rpc",
        "@capnp-cpp//src/capnp:capnpc",
        "@capnp-cpp//src/capnp/compat:http-over-capnp",
    ],
)

wd_cc_library(
    name = "actor-id",
    hdrs = ["actor-id.h"],
    visibility = ["//visibility:public"],
)

wd_cc_library(
    name = "io-channels",
    hdrs = ["io-channels.h"],
    visibility = ["//visibility:public"],
    deps = [
        ":actor-id",
        ":trace",
    ],
)

genrule(
    name = "trimmed-supported-compatibility-date-gen",
    srcs = ["supported-compatibility-date.txt"],
    outs = ["trimmed-supported-compatibility-date.txt"],
    cmd = "tr -d '\n' < $(location supported-compatibility-date.txt) > $(location trimmed-supported-compatibility-date.txt)",
    cmd_ps = "(Get-Content $(location supported-compatibility-date.txt) -Raw -Encoding Ascii).TrimEnd() | Set-Content $(location trimmed-supported-compatibility-date.txt) -NoNewLine -Encoding Ascii",
)

capnp_embed(
    name = "trimmed-supported-compatibility-date",
    src = ":trimmed-supported-compatibility-date-gen",
)

# TODO(cleanup): Split this target up further, as many dependents don't need all the files here.
#   Avoiding the dependency on supported-compatibility-date.capnp is often helpful due to the
#   transitive dependency on `trimmed-supported-compatibility-date.txt` which is dynamically
#   generated.
wd_cc_capnp_library(
    name = "capnp",
    srcs = [
        "cdp.capnp",
        "supported-compatibility-date.capnp",
    ],
    visibility = ["//visibility:public"],
    deps = [
        ":compatibility_date_capnp",
        ":features_capnp",
        ":outcome_capnp",
        ":script_version_capnp",
        ":trimmed-supported-compatibility-date",
        "@capnp-cpp//src/capnp/compat:http-over-capnp_capnp",
    ],
)

wd_cc_capnp_library(
    name = "worker-interface_capnp",
    srcs = [
        "worker-interface.capnp",
    ],
    visibility = ["//visibility:public"],
    deps = [
        ":outcome_capnp",
        ":script_version_capnp",
        "@capnp-cpp//src/capnp/compat:http-over-capnp_capnp",
    ],
)

wd_cc_capnp_library(
    name = "actor-storage_capnp",
    srcs = [
        "actor-storage.capnp",
    ],
    visibility = ["//visibility:public"],
)

wd_cc_capnp_library(
    name = "outcome_capnp",
    srcs = ["outcome.capnp"],
    visibility = ["//visibility:public"],
)

wd_cc_capnp_library(
    name = "script_version_capnp",
    srcs = ["script-version.capnp"],
    visibility = ["//visibility:public"],
)

wd_cc_capnp_library(
    name = "compatibility_date_capnp",
    srcs = ["compatibility-date.capnp"],
    visibility = ["//visibility:public"],
)

wd_cc_capnp_library(
    name = "features_capnp",
    srcs = ["features.capnp"],
    visibility = ["//visibility:public"],
)

kj_test(
    src = "io-gate-test.c++",
    deps = [
        ":io-gate",
    ],
)

kj_test(
    src = "actor-cache-test.c++",
    deps = [
        ":actor",
        ":io-gate",
        "//src/workerd/util:test",
        "//src/workerd/util:test-util",
    ],
)

kj_test(
    src = "promise-wrapper-test.c++",
    deps = [":io"],
)

kj_test(
    src = "compatibility-date-test.c++",
    deps = [
        ":io",
        "@capnp-cpp//src/capnp:capnpc",
    ],
)

kj_test(
    src = "observer-test.c++",
    deps = [
        ":observer",
        "@capnp-cpp//src/capnp:capnpc",
    ],
)
