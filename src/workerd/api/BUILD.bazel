load("//:build/kj_test.bzl", "kj_test")
load("//:build/wd_cc_capnp_library.bzl", "wd_cc_capnp_library")
load("//:build/wd_cc_library.bzl", "wd_cc_library")
load("//:build/wd_test.bzl", "wd_test")

filegroup(
    name = "srcs",
    srcs = select({
        "//src/workerd/io:set_enable_experimental_webgpu": glob(
            ["**/*.c++"],
            exclude = [
                "html-rewriter.c++",
                "data-url.c++",
                "rtti.c++",
                "**/*test*.c++",
                "pyodide.c++",
                "pyodide/pyodide.c++",
            ],
        ),
        "//conditions:default": glob(
            ["**/*.c++"],
            exclude = [
                "html-rewriter.c++",
                "data-url.c++",
                "rtti.c++",
                "**/*test*.c++",
                "pyodide.c++",
                "pyodide/pyodide.c++",
                "gpu/*.c++",
            ],
        ),
    }),
    visibility = ["//visibility:public"],
)

api_header_exclusions = [
    "html-rewriter.h",
    "deferred-proxy.h",
    "data-url.h",
    "modules.h",
    "rtti.h",
    "**/*test*.h",
]

filegroup(
    name = "hdrs",
    srcs = select({
        "//src/workerd/io:set_enable_experimental_webgpu": glob(
            ["**/*.h"],
            exclude = api_header_exclusions,
        ),
        "//conditions:default": glob(
            ["**/*.h"],
            exclude = api_header_exclusions + ["gpu/*.h"],
        ),
    }),
    visibility = ["//visibility:public"],
)

wd_cc_library(
    name = "rtti",
    srcs = ["rtti.c++"],
    hdrs = [
        "modules.h",
        "rtti.h",
        "//src/pyodide:generated/pyodide_extra.capnp.h",
        "//src/workerd/server:workerd.capnp.h",
    ],
    visibility = ["//visibility:public"],
    deps = [
        ":html-rewriter",
        "//src/pyodide",
        "//src/pyodide:pyodide_extra_capnp",
        "//src/workerd/io",
        "//src/workerd/jsg:rtti",
        "//src/workerd/server:workerd_capnp",
        "//src/workerd/api/node",
    ],
)

# Moved out of io library so that it does not depend on lolhtml and the associated rust crates.
# This reduces the linker input size for tests based on io.
wd_cc_library(
    name = "html-rewriter",
    srcs = ["html-rewriter.c++"],
    hdrs = ["html-rewriter.h"],
    implementation_deps = [
        "@com_cloudflare_lol_html//:lolhtml",
    ],
    visibility = ["//visibility:public"],
    deps = [
        "//src/workerd/io",
    ],
)

wd_cc_library(
    name = "pyodide",
    srcs = [
        "pyodide/pyodide.c++",
    ],
    hdrs = [
        "pyodide/pyodide.h",
        "//src/pyodide:generated/pyodide_extra.capnp.h",
    ],
    visibility = ["//visibility:public"],
    deps = [
        "//src/pyodide",
        "//src/pyodide:pyodide_extra_capnp",
        "//src/workerd/io",
        "//src/workerd/jsg",
        "//src/workerd/server:workerd_capnp",
    ],
)

wd_cc_library(
    name = "data-url",
    srcs = ["data-url.c++"],
    hdrs = ["data-url.h"],
    visibility = ["//visibility:public"],
    deps = [
        "//src/workerd/jsg:url",
        "//src/workerd/util",
        "@capnp-cpp//src/kj",
    ],
)

wd_cc_library(
    name = "deferred-proxy",
    hdrs = ["deferred-proxy.h"],
    visibility = ["//visibility:public"],
    deps = [
        "@capnp-cpp//src/kj",
        "@capnp-cpp//src/kj:kj-async",
    ],
)

wd_cc_capnp_library(
    name = "r2-api_capnp",
    srcs = ["r2-api.capnp"],
    visibility = ["//visibility:public"],
)

wd_cc_capnp_library(
    name = "analytics-engine_capnp",
    srcs = ["analytics-engine.capnp"],
    visibility = ["//visibility:public"],
)

[kj_test(
    src = f,
    deps = [
        "//src/workerd/io",
    ],
) for f in [
    "actor-state-test.c++",
    "basics-test.c++",
    "crypto/aes-test.c++",
    "crypto/impl-test.c++",
    "streams/queue-test.c++",
    "streams/standard-test.c++",
    "util-test.c++",
    ]
]

kj_test(
    src = "data-url-test.c++",
    deps = [
        ":data-url",
    ],
)

kj_test(
    src = "deferred-proxy-test.c++",
    deps = [
        ":deferred-proxy",
    ],
)

kj_test(
    src = "streams/internal-test.c++",
    deps = [
        "//src/workerd/io",
        "//src/workerd/tests:test-fixture",
    ],
)

kj_test(
    src = "actor-state-iocontext-test.c++",
    deps = [
        "//src/workerd/io",
        "//src/workerd/tests:test-fixture",
    ],
)

kj_test(
    src = "api-rtti-test.c++",
    deps = [
        ":html-rewriter",
        "//src/workerd/io",
        "//src/workerd/jsg:rtti",
    ],
)

kj_test(
    src = "cf-property-test.c++",
    deps = ["//src/workerd/tests:test-fixture"],
)

[wd_test(
    src = f,
    args = ["--experimental"],
    data = [f.removesuffix(".wd-test") + ".js"],
) for f in glob(
    ["**/*.wd-test"],
    exclude = [
        "tests/js-rpc-socket-test.wd-test",
    ],
)]

wd_test(
    src = "tests/js-rpc-socket-test.wd-test",
    args = ["--experimental", "--no-verbose"],
    data = ["tests/js-rpc-test.js"],
)

[wd_test(
    src = f,
    data = [f.removesuffix(".ts-wd-test") + ".ts"],
    args = ["--experimental"],
) for f in glob(
    ["**/*.ts-wd-test"],
)]

# Enable GPU tests if experimental GPU support is enabled. Unfortunately, this depends on the right
# drivers being available on Linux and macOS, so turn it off by default. Run GPU tests with
# `bazel test //src/workerd/api:gpu/<test name>`.
[wd_test(
    src = f,
    args = ["--experimental"],
    data = [f.removesuffix(".gpu-wd-test") + ".js"],
    tags = ["off-by-default"],
    target_compatible_with = select({
        "//src/workerd/io:set_enable_experimental_webgpu": [],
        "//conditions:default": ["@platforms//:incompatible"],
    }),
) for f in glob(
    ["**/*.gpu-wd-test"],
)]
