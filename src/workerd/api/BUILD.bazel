load("//:build/wd_cc_capnp_library.bzl", "wd_cc_capnp_library")
load("//:build/kj_test.bzl", "kj_test")
load("//:build/wd_test.bzl", "wd_test")

filegroup(
    name = "srcs",
    srcs = select({
        "//src/workerd/io:set_enable_experimental_webgpu": glob(
            ["**/*.c++"],
            exclude = [
                "**/*test*.c++",
            ],
        ),
        "//conditions:default": glob(
            ["**/*.c++"],
            exclude = [
                "**/*test*.c++",
                "gpu/*.c++",
            ],
        ),
    }),
    visibility = ["//visibility:public"],
)

filegroup(
    name = "hdrs",
    srcs = select({
        "//src/workerd/io:set_enable_experimental_webgpu": glob(
            ["**/*.h"],
            exclude = [
                "**/*test*.h",
            ],
        ),
        "//conditions:default": glob(
            ["**/*.h"],
            exclude = [
                "**/*test*.h",
                "gpu/*.h",
            ],
        ),
    }),
    visibility = ["//visibility:public"],
)

wd_cc_capnp_library(
    name = "r2-api_capnp",
    srcs = ["r2-api.capnp"],
    visibility = ["//visibility:public"],
)

wd_cc_capnp_library(
    name = "analytics-engine_capnp",
    srcs = ["analytics-engine.capnp"],
    visibility = ["//visibility:public"],
)

[kj_test(
    src = f,
    deps = [
        "//src/workerd/io",
        "//src/workerd/jsg:rtti",
    ],
) for f in glob(
    ["**/*-test.c++"],
    exclude = [
        "node/*-test.c++",
    ],
)]

kj_test(
    src = "node/buffer-test.c++",
    deps = ["//src/workerd/tests:test-fixture"],
)

[wd_test(
    src = f,
    args = ["--experimental"],
    data = [f.removesuffix(".wd-test") + ".js"],
) for f in glob(
    ["**/*.wd-test"],
)]
