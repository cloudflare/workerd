load("@bazel_skylib//rules:run_binary.bzl", "run_binary")
load("@rules_rust//rust:defs.bzl", "rust_binary")
load("//:build/cc_ast_dump.bzl", "cc_ast_dump")
load("//:build/run_binary_target.bzl", "run_binary_target")
load("//:build/wd_cc_binary.bzl", "wd_cc_binary")
load("//:build/wd_cc_library.bzl", "wd_cc_library")

# ========================================================================================
# C++ deps for API extraction
#
# Both `api_encoder` and `param_extractor` need access to the API surface
# of `workerd`, so this target allows them to both have the same deps

wd_cc_library(
    name = "api_encoder_lib",
    deps = [
        "//src/workerd/api:html-rewriter",
        "//src/workerd/io",
        "//src/workerd/jsg",
        "//src/workerd/jsg:rtti",
        "@capnp-cpp//src/capnp",
    ],
    target_compatible_with = select({
        "@platforms//os:windows": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
)

api_encoder_src = "api-encoder.c++"

# ========================================================================================
# API Encoder
#
# Encodes runtime API type information into a capnp schema

wd_cc_binary(
    name = "api_encoder_bin",
    srcs = [api_encoder_src],
    deps = [":api_encoder_lib"],
    # Use dynamic linkage where possible to reduce binary size â€“ unlike the workerd binary, we
    # shouldn't need to distribute the api encoder.
    linkstatic = 0,
    # The dependent targets are not Windows-compatible, no need to compile this.
    target_compatible_with = select({
        "@platforms//os:windows": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
)

# All compatibility dates that changed public facing types.
# Remember to update `npm/workers-types/README.md` when adding new dates here.
compat_dates = [
    # Oldest compatibility date, with no flags enabled
    ("2021-01-01", "oldest"),
    # https://developers.cloudflare.com/workers/platform/compatibility-dates/#formdata-parsing-supports-file
    ("2021-11-03", "2021-11-03"),
    # https://developers.cloudflare.com/workers/platform/compatibility-dates/#settersgetters-on-api-object-prototypes
    ("2022-01-31", "2022-01-31"),
    # https://developers.cloudflare.com/workers/platform/compatibility-dates/#global-navigator
    ("2022-03-21", "2022-03-21"),
    # https://developers.cloudflare.com/workers/platform/compatibility-dates/#r2-bucket-list-respects-the-include-option
    ("2022-08-04", "2022-08-04"),
    # https://developers.cloudflare.com/workers/platform/compatibility-dates/#new-url-parser-implementation
    ("2022-10-31", "2022-10-31"),
    # https://developers.cloudflare.com/workers/platform/compatibility-dates/#streams-constructors
    # https://developers.cloudflare.com/workers/platform/compatibility-dates/#compliant-transformstream-constructor
    ("2022-11-30", "2022-11-30"),
    # https://github.com/cloudflare/workerd/blob/fcb6f33d10c71975cb2ce68dbf1924a1eeadbd8a/src/workerd/io/compatibility-date.capnp#L275-L280 (http_headers_getsetcookie)
    ("2023-03-01", "2023-03-01"),
    # https://github.com/cloudflare/workerd/blob/fcb6f33d10c71975cb2ce68dbf1924a1eeadbd8a/src/workerd/io/compatibility-date.capnp#L307-L312 (urlsearchparams_delete_has_value_arg)
    ("2023-07-01", "2023-07-01"),
    # Latest compatibility date (note these types should be the same as the previous entry)
    (None, "experimental"),
]

filegroup(
    name = "api_encoder",
    srcs = [
        "//src/workerd/tools:api_encoder_" + label
        for (date, label) in compat_dates
    ],
    tags = ["no-arm64"],
    target_compatible_with = select({
        "@platforms//os:windows": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
    visibility = ["//visibility:public"],
)

[
    run_binary_target(
        name = "api_encoder_" + label,
        outs = [label + ".api.capnp.bin"],
        args = [
            "--output",
            "$(location " + label + ".api.capnp.bin)",
        ] + ([
            "--compatibility-date",
            date,
        ] if date else []),
        # Cross-compiling is not supported as this runs in target cfg.
        tags = ["no-arm64"],
        target_compatible_with = select({
            "@platforms//os:windows": ["@platforms//:incompatible"],
            "//conditions:default": [],
        }),
        tool = "api_encoder_bin",
        visibility = ["//visibility:public"],
    )
    for (date, label) in compat_dates
]

# ========================================================================================
# Parameter Name Extractor
#
# Extracts the parameter names of functions, methods, etc. of the runtime API,
# since they're not encoded in the type information generated by `api_encoder`

cc_library(
    name = "compile_api_headers_only",
    defines = ["API_ENCODER_HDRS_ONLY=1"],
)

cc_ast_dump(
    name = "dump_api_ast",
    src = api_encoder_src,
    out = "api.ast.json.gz",
    target_compatible_with = select({
        "@platforms//os:windows": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
    deps = [
        ":api_encoder_lib",
        ":compile_api_headers_only",
    ],
)

rust_binary(
    name = "param_extractor_bin",
    srcs = ["param-extractor.rs"],
    tags = ["no-downstream"],
    target_compatible_with = select({
        "@platforms//os:windows": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
    deps = [
        "@crates_vendor//:anyhow",
        "@crates_vendor//:clang-ast",
        "@crates_vendor//:flate2",
        "@crates_vendor//:pico-args",
        "@crates_vendor//:serde",
        "@crates_vendor//:serde_json",
    ],
)

# Deliberately not marking this run_binary_target(): The exec configuration compiles with
# optimization by default, causing it to run markedly faster than the (non-opt) target
# configuration, where param_extractor would otherwise take several minutes to run. A different
# approach would be to set `rustc_flags = ["-C", "opt-level=3"],` here, although in this case any
# dependencies shared with the rust-deps target would still need to be compiled twice.
run_binary(
    name = "param_extractor",
    srcs = [
        ":dump_api_ast",
    ],
    outs = ["param-names.json"],
    args = [
        "--input",
        "$(location dump_api_ast)",
        "--output",
        "$(location param-names.json)",
    ],
    tags = ["no-downstream"],
    target_compatible_with = select({
        "@platforms//os:windows": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
    tool = "param_extractor_bin",
    visibility = ["//visibility:public"],
)
