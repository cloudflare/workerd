load("//src/workerd/server/tests/python:import_tests.bzl", "gen_rust_import_tests")
load("//src/workerd/server/tests/python:py_wd_test.bzl", "py_wd_test", "python_test_setup")

python_test_setup()

# Each test takes a while to start up, so if possible add additional tests to many-tests or sdk or
# some other existing test.
[py_wd_test(directory) for directory in [
    "hello",
    "many-tests",
    "asgi",
    "asgi-sse",
    "random",
    "sdk",
    "python-rpc",
    "workflow-entrypoint",
    "worker-entrypoint",
    "durable-object",
]]

# These tests all work fine without make_snapshot=False
# but it saves some time and does not lose any coverage.
[py_wd_test(
    directory,
    make_snapshot = False,
) for directory in [
    "env-param",
    "subdirectory",
    "undefined-handler",
    "vendor_dir",
    "vendor_dir_compat_flag",
    "filter-non-py-files",
    "default-class-with-legacy-global-handlers",
]]

gen_rust_import_tests()

py_wd_test(
    "dont-snapshot-pyodide",
    # Passes on newer Python versions but it tests a 0.26.0a2-specific behavior.
    python_flags = ["0.26.0a2"],
)

py_wd_test(
    "durable-object-websocket",
    make_snapshot = False,
    # TODO: enabling this will reproduce a strange issue, see comment in EW-9767.
    use_snapshot = None,
)

py_wd_test(
    "jspi",
    skip_python_flags = ["0.26.0a2"],
)

py_wd_test(
    "fastapi",
    make_snapshot = False,
    use_snapshot = "fastapi",
)

py_wd_test(
    "numpy",
    make_snapshot = False,
    use_snapshot = "numpy",
)

py_wd_test("python-compat-flag")
