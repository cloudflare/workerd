load("@bazel_skylib//lib:selects.bzl", "selects")
load("@bazel_skylib//rules:common_settings.bzl", "bool_flag")
load("@bazel_skylib//rules:copy_file.bzl", "copy_file")
load("//:build/kj_test.bzl", "kj_test")
load("//:build/wd_cc_binary.bzl", "wd_cc_binary")
load("//:build/wd_cc_capnp_library.bzl", "wd_cc_capnp_library")
load("//:build/wd_cc_library.bzl", "wd_cc_library")

config_setting(
    name = "is_linux",
    constraint_values = ["@platforms//os:linux"],
)

# Flag that can be used to force-disable tcmalloc. Mainly used for ASAN builds.
# TODO(cleanup): This feels ugly but I've exceeded my timebox for fighting Bazel for now.
bool_flag(
    name = "use_tcmalloc",
    build_setting_default = True,
)

config_setting(
    name = "set_use_tcmalloc",
    flag_values = {"use_tcmalloc": "True"},
)

selects.config_setting_group(
    name = "really_use_tcmalloc",
    match_all = [
        ":set_use_tcmalloc",
        ":is_linux",
    ],
)

# current malloc in use
wd_cc_library(
    name = "malloc",
    visibility = ["//visibility:public"],
    deps = select({
        ":really_use_tcmalloc": ["@com_google_tcmalloc//tcmalloc"],
        "//conditions:default": ["@bazel_tools//tools/cpp:malloc"],
    }),
)

wd_cc_binary(
    name = "workerd",
    srcs = ["workerd.c++"],
    malloc = ":malloc",
    tags = ["no-arm64"],
    visibility = ["//visibility:public"],
    deps = [
        ":server",
        ":workerd-meta_capnp",
        ":workerd_capnp",
        "//src/pyodide:pyodide_extra_capnp",
        "//src/workerd/util:autogate",
        "//src/workerd/util:perfetto",
        "@capnp-cpp//src/capnp:capnpc",
    ] + select({
        "@platforms//os:windows": [],
        "//conditions:default": ["@workerd//src/workerd/util:symbolizer"],
    }),
)

wd_cc_library(
    name = "alarm-scheduler",
    srcs = [
        "alarm-scheduler.c++",
    ],
    hdrs = [
        "alarm-scheduler.h",
    ],
    visibility = ["//visibility:public"],
    deps = [
        "//src/workerd/io",
        "//src/workerd/util:sqlite",
        "@capnp-cpp//src/kj",
        "@capnp-cpp//src/kj:kj-async",
    ],
)

wd_cc_library(
    name = "actor-id-impl",
    srcs = [
        "actor-id-impl.c++",
    ],
    hdrs = [
        "actor-id-impl.h",
    ],
    visibility = ["//visibility:public"],
    deps = [
        "//src/workerd/io:actor-id",
        "//src/workerd/jsg:exception",
        "//src/workerd/util:thread-scopes",
        "@capnp-cpp//src/kj:kj",
        "@ssl",
    ],
)

wd_cc_library(
    name = "server",
    srcs = [
        "server.c++",
        "v8-platform-impl.c++",
        "workerd-api.c++",
    ],
    hdrs = [
        "server.h",
        "v8-platform-impl.h",
        "workerd-api.h",
    ],
    defines = select({
        "//src/workerd/io:set_enable_experimental_webgpu": ["WORKERD_EXPERIMENTAL_ENABLE_WEBGPU"],
        "//conditions:default": [],
    }),
    visibility = ["//visibility:public"],
    deps = [
        ":actor-id-impl",
        ":alarm-scheduler",
        ":workerd_capnp",
        "//src/workerd/api:html-rewriter",
        "//src/workerd/api:pyodide",
        "//src/workerd/api:rtti",
        "//src/workerd/io",
        "//src/workerd/io:worker-entrypoint",
        "//src/workerd/jsg",
        "//src/workerd/util:perfetto",
        "@capnp-cpp//src/kj/compat:kj-tls",
    ],
)

wd_cc_capnp_library(
    name = "workerd_capnp",
    srcs = ["workerd.capnp"],
    visibility = ["//visibility:public"],
    deps = [],
)

wd_cc_capnp_library(
    name = "workerd-meta_capnp",
    srcs = ["workerd-meta.capnp"],
    visibility = ["//visibility:public"],
    deps = [
        ":workerd_capnp",
    ],
)

sh_test(
    name = "helloworld_compile_test",
    size = "small",
    srcs = ["tests/compile-tests/compile-test.sh"],
    args = [
        "$(location :workerd)",
        "$(location //samples:helloworld/config.capnp)",
        "$(location tests/compile-tests/compile-helloworld-test.ok)",
    ],
    data = [
        "tests/compile-tests/compile-helloworld-test.ok",
        ":workerd",
        "//samples:helloworld/config.capnp",
        "//samples:helloworld/worker.js",
    ],
    tags = ["no-qemu"],
)

kj_test(
    src = "server-test.c++",
    deps = [
        ":server",
        "//src/workerd/util:test-util",
    ],
)

kj_test(
    src = "actor-id-impl-test.c++",
    deps = [
        ":actor-id-impl",
        "//src/workerd/jsg:exception",
        "//src/workerd/util:thread-scopes",
        "@capnp-cpp//src/kj",
        "@ssl",
    ],
)


copy_file(
    name = "pyodide.capnp.bin@rule",
    src = "//src/pyodide:pyodide.capnp.bin@rule",
    out = "pyodide.capnp.bin",
)

