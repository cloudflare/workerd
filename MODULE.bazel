"Bazel dependencies, see https://registry.bazel.build"

module(name = "workerd")

bazel_dep(name = "zlib", version = "1.3.1.bcr.7")
git_override(
    module_name = "zlib",
    build_file = "//:build/BUILD.zlib",
    # This should match the version specified in V8 DEPS, but in practice it is generally acceptable
    # for it to be behind â€“ zlib is very stable and its API has not changed in a long time, most
    # changes to the Chromium fork affect ancillary tools and not the zlib library itself.
    commit = "85f05b0835f934e52772efc308baa80cdd491838",
    patch_args = ["-p1"],
    patches = ["//:patches/zlib/0001-Add-dummy-MODULE.bazel.patch"],
    remote = "https://chromium.googlesource.com/chromium/src/third_party/zlib.git",
)

# BoringSSL may subtly break backwards compatibility and behave differently than the latest FIPS
# version, often by rejecting key values that it considers invalid/unsafe even though they are still
# accepted by BoringSSL. Update with caution and only after confirming this is compatible with the
# downstream build.
bazel_dep(name = "boringssl", version = "0.20251002.0", repo_name = "ssl")
archive_override(
    module_name = "boringssl",
    patch_strip = 1,
    patches = [
        "//:patches/boringssl/0001-Expose-libdecrepit-so-NodeJS-can-use-it-for-ncrypto.patch",
    ],
    sha256 = "f96733fc3df03d4195db656d1b7b8c174c33f95d052f811f0ecc8f4e4e3db332",
    strip_prefix = "boringssl-0.20251002.0",
    type = "tgz",
    urls = ["https://github.com/google/boringssl/archive/refs/tags/0.20251002.0.tar.gz"],
)

include("//build/deps:deps.MODULE.bazel")
include("//build/deps:nodejs.MODULE.bazel")
include("//build/deps:python.MODULE.bazel")
include("//build/deps:v8.MODULE.bazel")
include("//build/deps:oci.MODULE.bazel")

# Automatically managed dependencies
include("//build/deps:gen/deps/deps.MODULE.bazel")
include("//build/deps:gen/build_deps/build_deps.MODULE.bazel")
include("//build/deps:gen/shared_deps/shared_deps.MODULE.bazel")

# Must be loaded after automated dependencies, which provide rules_rust
include("//build/deps:rust.MODULE.bazel")

# compatibility proxy is needed when using current rules_cc with Bazel 8
compat = use_extension("@rules_cc//cc:extensions.bzl", "compatibility_proxy")
use_repo(compat, "cc_compatibility_proxy")

cc_configure = use_extension("@rules_cc//cc:extensions.bzl", "cc_configure_extension")
use_repo(cc_configure, "local_config_cc")
