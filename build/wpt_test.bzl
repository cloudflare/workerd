# Copyright (c) 2017-2022 Cloudflare, Inc.
# Licensed under the Apache 2.0 license found in the LICENSE file or at:
#     https://opensource.org/licenses/Apache-2.0

load("//:build/wd_test.bzl", "wd_test")

def wpt_test(name, wpt_directory, test_config):
    """
    Main entry point.
    1. Generates a workerd test suite in JS. This contains the logic to run
       each WPT test file, applying the relevant test config.
    2. Generates a wd-test file for this test suite. This contains all of the
       paths to modules needed to run the test: generated test suite, test config
       file, WPT test scripts, associated JSON resources.
    """

    js_test_gen_rule = "{}@_wpt_js_test_gen".format(name)
    test_config_as_js = test_config.removesuffix(".ts") + ".js"

    _wpt_js_test_gen(
        name = js_test_gen_rule,
        test_name = name,
        wpt_directory = wpt_directory,
        test_config = test_config_as_js,
    )

    wd_test_gen_rule = "{}@_wpt_wd_test_gen".format(name)
    _wpt_wd_test_gen(
        name = wd_test_gen_rule,
        test_name = name,
        wpt_directory = wpt_directory,
        test_config = test_config_as_js,
        test_js_generated = js_test_gen_rule,
    )

    wd_test(
        name = "{}".format(name),
        src = wd_test_gen_rule,
        args = ["--experimental"],
        ts_deps = ["//src/wpt:wpt-test-harness"],
        data = [
            "//src/wpt:wpt-test-harness",
            test_config,
            js_test_gen_rule,
            wpt_directory,
            "//src/workerd/io:trimmed-supported-compatibility-date.txt",
        ],
    )

def _wpt_js_test_gen_impl(ctx):
    """
    Generates a workerd test suite in JS. This contains the logic to run
    each WPT test file, applying the relevant test config.
    """

    src = ctx.actions.declare_file("{}-test.generated.js".format(ctx.attr.test_name))
    ctx.actions.write(
        output = src,
        content = WPT_JS_TEST_TEMPLATE.format(
            test_config = ctx.file.test_config.basename,
            cases = generate_external_cases(ctx.attr.wpt_directory.files),
        ),
    )

    return DefaultInfo(
        files = depset([src]),
    )

def generate_external_cases(files):
    """
    Generate a workerd test case that runs each test file in the WPT module.
    """

    result = []

    for file in files.to_list():
        if file.extension == "js":
            entry = """export const {} = run(config, '{}');""".format(test_case_name(file.basename), file.basename)
            result.append(entry)

    return "\n".join(result)

def test_case_name(filename):
    """
    Converts a JS filename to a valid JS identifier for use as a test case name.
    WPT files are named with the convention some-words-with-hyphens.some-suffix.js.
    We would turn this into someWordsWithHyphensSomeSuffix.
    """

    words = (filename
        .removesuffix(".js")
        .removesuffix(".any")
        .replace(".", "-")
        .split("-"))

    return words[0] + "".join([word.capitalize() for word in words[1:]])

WPT_JS_TEST_TEMPLATE = """// This file is autogenerated by wpt_test.bzl
// DO NOT EDIT.
import {{ run }} from 'wpt:harness';
import config from '{test_config}';

{cases}
"""

def _wpt_wd_test_gen_impl(ctx):
    """
    Generates a wd-test file for this test suite. This contains all of the
    paths to modules needed to run the test: generated test suite, test config
    file, WPT test scripts, associated JSON resources.
    """

    src = ctx.actions.declare_file("{}.wd-test".format(ctx.attr.test_name))
    ctx.actions.write(
        output = src,
        content = WPT_WD_TEST_TEMPLATE.format(
            test_name = ctx.attr.test_name,
            test_config = ctx.file.test_config.basename,
            test_js_generated = wd_relative_path(ctx.file.test_js_generated),
            modules = generate_external_modules(ctx.attr.wpt_directory.files),
        ),
    )

    return DefaultInfo(
        files = depset([src]),
    )

WPT_WD_TEST_TEMPLATE = """
using Workerd = import "/workerd/workerd.capnp";
const unitTests :Workerd.Config = (
  services = [
    ( name = "{test_name}",
      worker = (
        modules = [
          (name = "worker", esModule = embed "{test_js_generated}"),
          (name = "{test_config}", esModule = embed "{test_config}"),
          (name = "wpt:harness", esModule = embed "../../../../../workerd/src/wpt/harness.js"),
          {modules}
        ],
        bindings = [
          (name = "wpt", service = "wpt"),
        ],
        compatibilityDate = embed "../../../../../workerd/src/workerd/io/trimmed-supported-compatibility-date.txt",
        compatibilityFlags = ["nodejs_compat", "experimental"],
      )
    ),
    (
      name = "wpt",
      disk = ".",
    )
  ],
);"""

def wd_relative_path(file):
    """
    Returns a relative path which can be referenced in the .wd-test file.
    This is four directories up from the bazel short_path
    """

    return "../" * 4 + file.short_path

def generate_external_modules(files):
    """
    Generates a string for all files in the given directory in the specified format.
    Example for a JS file:
        (name = "url-origin.any.js", esModule = embed "../../../../../wpt/url/url-origin.any.js"),
    Example for a JSON file:
        (name = "resources/urltestdata.json", json = embed "../../../../../wpt/url/resources/urltestdata.json"),
    """

    result = []

    for file in files.to_list():
        file_path = wd_relative_path(file)
        if file.extension == "js":
            entry = """(name = "{}", esModule = embed "{}")""".format(file.basename, file_path)
        elif file.extension == "json":
            # TODO(soon): It's difficult to manipulate paths in Bazel, so we assume that all JSONs are in a resources/ directory for now
            entry = """(name = "resources/{}", json = embed "{}")""".format(file.basename, file_path)
        else:
            # For other file types, you can add more conditions or skip them
            continue

        result.append(entry)

    return ",\n".join(result)

_wpt_wd_test_gen = rule(
    implementation = _wpt_wd_test_gen_impl,
    attrs = {
        # A string to use as the test name. Used in the wd-test filename and the worker's name
        "test_name": attr.string(),
        # A file group representing a directory of wpt tests. All files in the group will be embedded.
        "wpt_directory": attr.label(),
        # A JS file containing the test configuration.
        "test_config": attr.label(allow_single_file = True),
        # An auto-generated JS file containing the test logic.
        "test_js_generated": attr.label(allow_single_file = True),
    },
)

_wpt_js_test_gen = rule(
    implementation = _wpt_js_test_gen_impl,
    attrs = {
        # A string to use as the test name. Used in the wd-test filename and the worker's name
        "test_name": attr.string(),
        # A file group representing a directory of wpt tests. All files in the group will be embedded.
        "wpt_directory": attr.label(),
        # A JS file containing the test configuration.
        "test_config": attr.label(allow_single_file = True),
    },
)
