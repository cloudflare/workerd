# Copyright (c) 2017-2022 Cloudflare, Inc.
# Licensed under the Apache 2.0 license found in the LICENSE file or at:
#     https://opensource.org/licenses/Apache-2.0

load("//:build/wd_test.bzl", "wd_test")

def wpt_test(name, wpt_directory, test_config):
    """
    Main entry point.
    1. Generates a workerd test suite in JS. This contains the logic to run
       each WPT test file, applying the relevant test config.
    2. Generates a wd-test file for this test suite. This contains all of the
       paths to modules needed to run the test: generated test suite, test config
       file, WPT test scripts, associated JSON resources.
    """

    js_test_gen_rule = "{}@_wpt_js_test_gen".format(name)
    test_config_as_js = test_config.removesuffix(".ts") + ".js"
    harness = "//src/wpt:wpt-test-harness"
    compat_date = "//src/workerd/io:trimmed-supported-compatibility-date.txt"

    _wpt_js_test_gen(
        name = js_test_gen_rule,
        test_name = name,
        wpt_directory = wpt_directory,
        test_config = test_config_as_js,
    )

    wd_test_gen_rule = "{}@_wpt_wd_test_gen".format(name)
    _wpt_wd_test_gen(
        name = wd_test_gen_rule,
        test_name = name,
        wpt_directory = wpt_directory,
        test_config = test_config_as_js,
        test_js_generated = js_test_gen_rule,
        harness = harness,
        compat_date = compat_date,
    )

    wd_test(
        name = "{}".format(name),
        src = wd_test_gen_rule,
        args = ["--experimental"],
        ts_deps = [harness],
        data = [
            harness,
            test_config,
            js_test_gen_rule,
            wpt_directory,
            compat_date,
        ],
    )

def wpt_get_directories(root, excludes = []):
    """
    Globs for files within a WPT directory structure, starting from root.
    In addition to an explicitly provided excludes argument, hidden directories
    and top-level files are also excluded as they don't contain test content.
    """

    root_pattern = "{}/*".format(root) if root else "*"
    return native.glob(
        [root_pattern],
        exclude = native.glob(
            [root_pattern],
            exclude_directories = 1,
        ) + [".*"] + excludes,
        exclude_directories = 0,
    )

def _wpt_js_test_gen_impl(ctx):
    """
    Generates a workerd test suite in JS. This contains the logic to run
    each WPT test file, applying the relevant test config.
    """

    src = ctx.actions.declare_file("{}-test.generated.js".format(ctx.attr.test_name))
    ctx.actions.write(
        output = src,
        content = WPT_JS_TEST_TEMPLATE.format(
            test_config = ctx.file.test_config.basename,
            cases = generate_external_cases(ctx.attr.wpt_directory.files),
        ),
    )

    return DefaultInfo(
        files = depset([src]),
    )

def generate_external_cases(files):
    """
    Generate a workerd test case that runs each test file in the WPT module.
    """

    result = []

    for file in files.to_list():
        if file.extension == "js":
            entry = """export const {} = run('{}');""".format(test_case_name(file.basename), file.basename)
            result.append(entry)

    return "\n".join(result)

def test_case_name(filename):
    """
    Converts a JS filename to a valid JS identifier for use as a test case name.
    WPT files are named with the convention some-words-with-hyphens.some-suffix.js.
    We would turn this into someWordsWithHyphensSomeSuffix.
    """

    words = (filename
        .removesuffix(".js")
        .removesuffix(".any")
        .replace(".", "-")
        .split("-"))

    return words[0] + "".join([word.capitalize() for word in words[1:]])

WPT_JS_TEST_TEMPLATE = """// This file is autogenerated by wpt_test.bzl
// DO NOT EDIT.
import {{ createRunner }} from 'wpt:harness';
import config from '{test_config}';

const run = createRunner(config);

{cases}
"""

def _wpt_wd_test_gen_impl(ctx):
    """
    Generates a wd-test file for this test suite. This contains all of the
    paths to modules needed to run the test: generated test suite, test config
    file, WPT test scripts, associated JSON resources.
    """
    src = ctx.actions.declare_file("{}.wd-test".format(ctx.attr.test_name))
    ctx.actions.write(
        output = src,
        content = WPT_WD_TEST_TEMPLATE.format(
            test_name = ctx.attr.test_name,
            test_config = ctx.file.test_config.basename,
            test_js_generated = ctx.file.test_js_generated.basename,
            bindings = generate_external_bindings(ctx.file.test_config, ctx.attr.wpt_directory.files),
            harness = wd_relative_path(ctx.file.test_config, ctx.file.harness),
            compat_date = wd_relative_path(ctx.file.test_config, ctx.file.compat_date),
        ),
    )

    return DefaultInfo(
        files = depset([src]),
    )

WPT_WD_TEST_TEMPLATE = """
using Workerd = import "/workerd/workerd.capnp";
const unitTests :Workerd.Config = (
  services = [
    ( name = "{test_name}",
      worker = (
        modules = [
          (name = "worker", esModule = embed "{test_js_generated}"),
          (name = "{test_config}", esModule = embed "{test_config}"),
          (name = "wpt:harness", esModule = embed "{harness}"),
        ],
        bindings = [
          (name = "wpt", service = "wpt"),
          (name = "unsafe", unsafeEval = void),
          {bindings}
        ],
        compatibilityDate = embed "{compat_date}",
        compatibilityFlags = ["nodejs_compat", "experimental"],
      )
    ),
    (
      name = "wpt",
      disk = ".",
    )
  ],
);"""

def wd_relative_path(origin, target):
    """
    Generates a relative path for use in wd-test files.
    The origin is the directory containing the wd-test file, and the target is the file that should be referenced from it.
    """

    return "../" * origin.short_path.count("/") + target.short_path

def generate_external_bindings(origin, files):
    """
    Generates appropriate bindings for each file in the WPT module:
    - JS files: text binding to allow code to be evaluated
    - JSON files: JSON binding to allow test code to fetch resources
    """

    result = []

    for file in files.to_list():
        file_path = wd_relative_path(origin, file)
        if file.extension == "js":
            entry = """(name = "{}", text = embed "{}")""".format(file.basename, file_path)
        elif file.extension == "json":
            # TODO(soon): It's difficult to manipulate paths in Bazel, so we assume that all JSONs are in a resources/ directory for now
            entry = """(name = "resources/{}", json = embed "{}")""".format(file.basename, file_path)
        else:
            # For other file types, you can add more conditions or skip them
            continue

        result.append(entry)

    return ",\n".join(result)

_wpt_wd_test_gen = rule(
    implementation = _wpt_wd_test_gen_impl,
    attrs = {
        # A string to use as the test name. Used in the wd-test filename and the worker's name
        "test_name": attr.string(),
        # A file group representing a directory of wpt tests. All files in the group will be embedded.
        "wpt_directory": attr.label(),
        # A JS file containing the test configuration.
        "test_config": attr.label(allow_single_file = True),
        # An auto-generated JS file containing the test logic.
        "test_js_generated": attr.label(allow_single_file = True),
        # Target specifying the location of the WPT test harness
        "harness": attr.label(allow_single_file = True),
        # Target specifying the location of the trimmed-supported-compatibility-date.txt file
        "compat_date": attr.label(allow_single_file = True),
    },
)

_wpt_js_test_gen = rule(
    implementation = _wpt_js_test_gen_impl,
    attrs = {
        # A string to use as the test name. Used in the wd-test filename and the worker's name
        "test_name": attr.string(),
        # A file group representing a directory of wpt tests. All files in the group will be embedded.
        "wpt_directory": attr.label(),
        # A JS file containing the test configuration.
        "test_config": attr.label(allow_single_file = True),
    },
)
